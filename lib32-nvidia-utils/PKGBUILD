# Based on the file created for Arch Linux by:
# Thomas Baechler <thomas@archlinux.org>
# James Rayner <iphitus@gmail.com>

# Maintainer: Philip MÃ¼ller <philm@manjarolinux.org>

_pkgbasename=nvidia-utils
pkgname=lib32-$_pkgbasename
pkgver=313.30
pkgrel=1
pkgdesc="NVIDIA drivers utilities and libraries. (32-bit)"
arch=('x86_64')
url="http://www.nvidia.com/"
depends=('lib32-libxvmc' 'lib32-zlib' 'lib32-gcc-libs' 'lib32-libgl' 'mhwd' $_pkgbasename)
license=('custom')
options=('!strip')
install=$_pkgbasename.install

_arch='x86'
_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
source=("http://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
sha256sums=('0a0f10979b1bca8e4a244e204b58aeb9a580924a9dd7b11b21d15abfe1ad49ad')

build() {
    cd "${srcdir}"
    sh ${_pkg}.run --extract-only
}

package() {
    cd "${srcdir}/${_pkg}"

    # OpenGL library
    install -D -m755 libGL.so.${pkgver} "${pkgdir}/usr/lib32/libGL.so.nvidia"
    # OpenGL core library
    install -D -m755 libnvidia-glcore.so.${pkgver} "${pkgdir}/usr/lib32/libnvidia-glcore.so.${pkgver}"
    # XvMC
    #install -D -m644 libXvMCNVIDIA.a "${pkgdir}/usr/lib32/libXvMCNVIDIA.a"
    #install -D -m755 libXvMCNVIDIA.so.${pkgver} "${pkgdir}/usr/lib32/libXvMCNVIDIA.so.${pkgver}"
    # VDPAU
    install -D -m755 libvdpau_nvidia.so.${pkgver} "${pkgdir}/usr/lib32/vdpau/libvdpau_nvidia.so.${pkgver}"
    # CUDA
    install -D -m755 libcuda.so.${pkgver} "${pkgdir}/usr/lib32/libcuda.so.${pkgver}"
    install -D -m755 libnvcuvid.so.${pkgver} "${pkgdir}/usr/lib32/libnvcuvid.so.${pkgver}"
    # nvidia-tls library
    install -D -m755 tls/libnvidia-tls.so.${pkgver} "${pkgdir}/usr/lib32/libnvidia-tls.so.${pkgver}"
    # OpenCL
    install -D -m755 libnvidia-compiler.so.${pkgver} "${pkgdir}/usr/lib32/libnvidia-compiler.so.${pkgver}"
    install -D -m755 libOpenCL.so.1.0.0 "${pkgdir}/usr/lib32/libOpenCL.so.1.0.0"

    install -D -m755 libnvidia-cfg.so.${pkgver} "${pkgdir}/usr/lib32/libnvidia-cfg.so.${pkgver}"
    install -D -m755 libnvidia-ml.so.${pkgver} "${pkgdir}/usr/lib32/libnvidia-ml.so.${pkgver}"

    # create soname links
    for _lib in $(find "${pkgdir}" -name '*.so*'); do
        _soname="$(dirname ${_lib})/$(readelf -d "$_lib" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')"
        if [ ! -e "${_soname}" ]; then
            ln -s "$(basename ${_lib})" "${_soname}"
            ln -s "$(basename ${_soname})" "${_soname/.[0-9]*/}"
        fi
    done

    rm -rf "${pkgdir}"/usr/{include,share,bin}
    mkdir -p "${pkgdir}/usr/share/licenses"
    ln -s $_pkgbasename "${pkgdir}/usr/share/licenses/${pkgname}"

    # Bumblebee support
    mkdir -p "${pkgdir}/usr/lib32/nvidia-bumblebee"
    ln -s "/usr/lib32/libGL.so.nvidia" "${pkgdir}/usr/lib32/nvidia-bumblebee/libGL.so"
	ln -s "/usr/lib32/libGL.so.nvidia" "${pkgdir}/usr/lib32/nvidia-bumblebee/libGL.so.1"
	ln -s "/usr/lib32/libGL.so.nvidia" "${pkgdir}/usr/lib32/nvidia-bumblebee/libGL.so.1.2"

    # some cleanup
    cd "${pkgdir}/usr/lib32/"
    rm -f `ls ./ | grep libGL | grep -v libGL.so.nvidia`
}
