# Based on the file created for Arch Linux by:
# Dave Reisner <dreisner@archlinux.org>

# Maintainer:  Phil MÃ¼ller <philm@manajro.org>
# Contributor: Guinux <nuxgui@gmail.com>

pkgname=lib32-systemd-next
_pkgbasename=systemd
pkgver=201
pkgrel=1
pkgdesc="system and service manager (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
makedepends=('lib32-gcc-libs' 'gcc-multilib' 'lib32-dbus-core' 'lib32-libcap' 'lib32-acl' 
             'lib32-libgcrypt' 'lib32-glib2' 'lib32-xz' 'intltool' 'gperf')
options=('!libtool')
provides=("lib32-systemd=$pkgver")
conflicts=('lib32-systemd')
source=("http://www.freedesktop.org/software/$_pkgbasename/$_pkgbasename-$pkgver.tar.xz"
        use-split-usr-path.patch)
sha256sums=('d6299f1a0bc2122002d731808d3825c6b566d1ffe899c03ed5ebec7e70c8cc10'
            'bb0336f5956f93c4c3ac8f5b798956a54e071d6c03e93fb73d45268bf572b8b2')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/systemd-${pkgver}"

  # hang onto this until we do the /{,s}bin merge
  patch -Np1 <"$srcdir/use-split-usr-path.patch"

  ./configure \
    --enable-static \
    --libexecdir=/usr/lib32 \
    --libdir=/usr/lib32 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-audit \
    --disable-ima \
    --disable-pam \
    --disable-kmod \
    --disable-blkid \
    --without-python \
    --disable-libcryptsetup \
    --disable-tcpwrap \
    --disable-qrencode \
    --disable-microhttpd

  make
}

package() {
  depends=('lib32-glib2' 'lib32-libgcrypt' 'lib32-xz' 'systemd')
  cd "${srcdir}/systemd-${pkgver}"
    
  make DESTDIR="$pkgdir" install
    
  rm -rf "${pkgdir}"/{etc,var}
  rm -rf "${pkgdir}"/usr/{bin,include,lib,share}
  rm -rf "${pkgdir}"/usr/lib32/libnss*
}
