# Partly based on original PKGBUILD by Mikko Seppala <t-r-a-y@mbnet.fi>
# Contributor: Manuel Gaul <inkaine@hotmail.com>
# Contributor: Vi0L0 <vi0l093@gmail.com>
# Contributor: Enverex & kidoz

_pkgsourcename=catalyst-utils
pkgname=lib32-$_pkgsourcename
pkgver=12.11
pkgrel=1
pkgdesc="AMD/ATI catalyst driver utilities and libraries. (32-bit)"
url="http://www.ati.amd.com"
arch=(x86_64)
license=('custom')
groups=('lib32')
depends=('lib32-libgl' 'lib32-libxext' 'lib32-libdrm' 'catalyst-utils')
conflicts=('lib32-catalyst-legacy-utils')
replaces=('lib32-ati-fglrx-utils' 'lib32-fglrx-utils')
source=("http://www2.ati.com/drivers/beta/amd-driver-installer-catalyst-${pkgver}-beta-x86.x86_64.zip")
sha256sums=('0c3e0c96b3daf8a6647ba2285374c96b7d57bc0ee69fbc7dc0c091cd482caffc')
install=${pkgname}.install

build() {
  cd ${srcdir}

  sh ./amd-driver-installer*.run --extract fglrx-install
}

package() {
  depends=("catalyst-utils=${pkgver}")

  install -dm755 "${pkgdir}"/usr/bin
  install -dm755 "${pkgdir}"/usr/lib32/{dri,xorg/modules/{dri,extensions/fglrx}}
  
  # binaries
  cd ${srcdir}/fglrx-install
  install -m755 arch/x86/usr/X11R6/bin/fgl_glxgears "${pkgdir}/usr/bin/fgl_glxgears32"
  install -m755 arch/x86/usr/X11R6/bin/fglrxinfo "${pkgdir}/usr/bin/fglrxinfo32"
  
  # dri/gl/... drivers
  cd ${srcdir}/fglrx-install/xpic/usr/X11R6/lib/modules
  install -m755 *.so "${pkgdir}/usr/lib32/xorg/modules/"
  cd ${srcdir}/fglrx-install/arch/x86/usr/X11R6/lib
  install -m755 modules/dri/fglrx_dri.so "${pkgdir}/usr/lib32/xorg/modules/dri/"
  ln -s /usr/lib32/xorg/modules/dri/fglrx_dri.so "${pkgdir}/usr/lib32/dri/"
  install -m755 fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/"
  install -m755 *.so* "${pkgdir}/usr/lib32/"
  install -m644 libAMDXvBA.cap "${pkgdir}/usr/lib32/"
  cd ${srcdir}/fglrx-install/arch/x86/usr/lib
  install -m755 *.so* "${pkgdir}/usr/lib32/"
  # needed symlinks
  ln -s libatiuki.so.1.0 "${pkgdir}/usr/lib32/libatiuki.so.1"
  ln -s fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/libGL.so.catalyst"
  ln -s libAMDXvBA.so.1.0 "${pkgdir}/usr/lib32/libAMDXvBA.so.1"
  ln -s libAMDXvBA.so.1.0 "${pkgdir}/usr/lib32/libAMDXvBA.so"
  ln -s libfglrx_dm.so.1.0 "${pkgdir}/usr/lib32/libfglrx_dm.so.1"
  
  # useful for 32 bits ?
  rm "${pkgdir}"/usr/lib32/lib{amdocl*,OpenCL}.so* 

  # licenses
  install -Dm644 "${srcdir}/fglrx-install/common/usr/share/doc/fglrx/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
}
